#!/usr/bin/env perl

use strict;
use warnings;
use routines;

use feature 'say';

use File::Basename;
use File::Find;
use File::Spec::Functions;
use Getopt::Long;
use Data::Object::Name;

use File::Path 'make_path';

fun options() {
  my $data = {};

  GetOptions(
    "help|h" => \$data->{help},
    "input|i=s" => \$data->{input},
    "output|o=s" => \$data->{output},
    "pattern|p=s" => \$data->{pattern},
    "template|t=s" => \$data->{template},
  );

  return $data;
}

method helptext() {
  return join "\n", "testauto [options]", <<"EOF";

  -h --help      Display help text.
  -t --template  Filepath for POD template.
  -p --pattern   Pattern to use to filter files.
  -o --output    Directory for .pod files.
  -i --input     Directory for .t files.

e.g. testauto -i t -o lib -t ./template.pod
EOF
}

fun main($helptext, $options) {
  require Test::Auto;

  my @files;

  if ($options->{help}) {
    say $helptext;
    exit 1;
  }

  my $input = $options->{input} || 't';
  my $output = $options->{output} || 'lib';
  my $pattern = $options->{pattern} || '.*';
  my $template = $options->{template};

  if ($template) {
    $ENV{TEST_AUTO_TEMPLATE} = $template;
  }

  find fun() {
    push @files, $File::Find::name if -f && /\.t$/
  }, $input;

  for my $file (sort grep /$pattern/, @files) {
    my $test = Test::Auto->new($file);
    my $parser = $test->parser;
    my $package = $parser->render('name');
    my $namer = Data::Object::Name->new($package);

    my $podfile = $package;
       $podfile =~ s/::/\//g;
       $podfile = catfile($output, "$podfile.pod");

    make_path(dirname($podfile));

    open my $fh, '>', $podfile or die "Error writing POD file, $!: $podfile";
    print $fh $test->document->render;
    close $fh;

    say "Created $podfile";
  }

  return;
}

main(helptext(), options());
